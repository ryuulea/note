'=== SJIS不可文字チェック（行単位→差分で逐字特定） ===
Public Sub CheckMailBody_SJIS()
    Const HEADER As String = "メール本文"
    Dim ws As Worksheet, outWs As Worksheet
    Dim hdrCell As Range, col As Long, startRow As Long, lastRow As Long
    Dim r As Long, txt As String, sjisTxt As String
    Dim i As Long, c As String, cp As Long
    Dim exportRowNo As Long, errCount As Long
    Dim b() As Byte
    
    Set ws = ActiveSheet
    Set outWs = Worksheets("チェック結果")
    
    ' 出力開始行
    exportRowNo = outWs.Cells(outWs.Rows.Count, "E").End(xlUp).Row + 1
    
    ' 「メール本文」列を探す
    Set hdrCell = ws.Cells.Find(What:=HEADER, LookAt:=xlWhole)
    If hdrCell Is Nothing Then
        MsgBox "列見出し「" & HEADER & "」が見つかりません。", vbExclamation
        Exit Sub
    End If
    col = hdrCell.Column
    
    startRow = 2
    lastRow = ws.Cells(ws.Rows.Count, col).End(xlUp).Row
    
    For r = startRow To lastRow
        txt = CStr(ws.Cells(r, col).Value)
        If txt <> "" Then
            ' 一整行Shift-JIS変換
            b = StrConv(txt, vbFromUnicode, 932)
            sjisTxt = StrConv(b, vbUnicode, 932)
            
            If sjisTxt <> txt Then
                ' 有差異，逐字比對
                For i = 1 To Len(txt)
                    c = Mid(txt, i, 1)
                    If Mid(sjisTxt, i, 1) <> c Then
                        cp = AscW(c)
                        outWs.Range("E" & exportRowNo).Value = c
                        outWs.Range("F" & exportRowNo).Value = _
                            "表行:" & r & " / 本文内位置:" & i & " / U+" & Hex(cp)
                        exportRowNo = exportRowNo + 1
                        errCount = errCount + 1
                    End If
                Next i
            End If
        End If
    Next r
    
    ' 結果
    If errCount = 0 Then
        MsgBox "本文中にSJIS不可文字は見つかりませんでした。", vbInformation
    Else
        MsgBox "本文中にSJIS不可文字が " & errCount & " 件見つかりました。" & vbCrLf & _
               "詳細はチェック結果シートをご確認ください。", vbExclamation
    End If
End Sub
