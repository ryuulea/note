'=== ⑥ メール本文チェック ===
Private Function CheckMailBody_Result(ws As Worksheet, headerRow As Long) As String
    Const HEADER As String = "メール本文"
    Const MAX_LEN As Long = 900
    Dim hdrCell As Range, col As Long, startRow As Long, lastRow As Long
    Dim r As Long, txt As String, L As Long
    Dim i As Long, c As String, cp As Long
    Dim errCount As Long, errList As String
    Dim exportRowNo As Long
    
    ' 表頭位置を取得
    Set hdrCell = ws.Rows(headerRow).Find(What:=HEADER, LookAt:=xlWhole)
    If hdrCell Is Nothing Then
        CheckMailBody_Result = "列見出し「" & HEADER & "」が見つかりません。"
        Exit Function
    End If
    
    col = hdrCell.Column
    startRow = headerRow + 2
    lastRow = ws.Cells(ws.Rows.Count, col).End(xlUp).Row
    
    exportRowNo = 2
    
    For r = startRow To lastRow
        txt = CStr(ws.Cells(r, col).Value)
        L = Len(txt)
        
        ' 文字数超過チェック
        If L > MAX_LEN Then
            errCount = errCount + 1
            errList = errList & "行 " & r & " : " & L & "文字超過" & vbCrLf
        End If
        
        ' 1文字ずつチェック
        For i = 1 To L
            c = Mid(txt, i, 1)
            cp = AscW(c)
            
            ' SJIS不可文字チェック
            If Not IsSjisSafe(cp) Then
                Worksheets("チェック結果").Range("E" & exportRowNo).Value = c
                Worksheets("チェック結果").Range("F" & exportRowNo).Value = "表行:" & r & " / 正文行:" & i & " / 文字コード:" & Hex(cp)
                exportRowNo = exportRowNo + 1
                
                errCount = errCount + 1
                errList = errList & "行 " & r & " 正文行 " & i & " にSJIS不可文字あり" & vbCrLf
            End If
        Next i
    Next r
    
    ' 判定結果
    If errCount = 0 Then
        CheckMailBody_Result = "OK (全て " & MAX_LEN & "文字以内)"
    Else
        CheckMailBody_Result = "エラー " & errCount & " 件 : ご確認ください"
    End If
End Function
