'=== ⑥ メール本文チェック ===
Private Function CheckMailBody_Result(ws As Worksheet, headerRow As Long) As String
    Const HEADER As String = "メール本文"
    Const MAX_LEN As Long = 900
    Dim hdrCell As Range, col As Long, startRow As Long, lastRow As Long
    Dim r As Long, txt As String, L As Long
    Dim errCount As Long
    Dim outWs As Worksheet
    Dim exportRowNo As Long
    
    ' 出力先シートを設定
    Set outWs = Worksheets("チェック結果")
    exportRowNo = outWs.Cells(outWs.Rows.Count, "E").End(xlUp).Row + 1
    
    ' 見出し列を探す
    Set hdrCell = ws.Rows(headerRow).Find(What:=HEADER, LookAt:=xlWhole)
    If hdrCell Is Nothing Then
        CheckMailBody_Result = "列見出し「" & HEADER & "」が見つかりません。"
        Exit Function
    End If
    col = hdrCell.Column
    
    startRow = headerRow + 2
    lastRow = ws.Cells(ws.Rows.Count, col).End(xlUp).Row
    
    ' === 本文チェック ===
    For r = startRow To lastRow
        txt = CStr(ws.Cells(r, col).Value)
        L = Len(txt)
        
        ' 900文字超過チェック
        If L > MAX_LEN Then
            errCount = errCount + 1
            outWs.Range("E" & exportRowNo).Value = "―"
            outWs.Range("F" & exportRowNo).Value = "表行:" & r & " / 文字数超過 (" & L & "文字)"
            exportRowNo = exportRowNo + 1
        End If
    Next r
    
    ' === 判定結果を返す ===
    If errCount = 0 Then
        CheckMailBody_Result = "OK（全て " & MAX_LEN & "文字以内）"
    Else
        CheckMailBody_Result = "エラー " & errCount & " 件 : ご確認ください"
    End If
End Function
