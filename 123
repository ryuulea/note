'=== メール本文 SJIS不可文字チェック ===
Public Sub CheckMailBody_SJIS()
    Const HEADER As String = "メール本文"
    Dim ws As Worksheet, outWs As Worksheet
    Dim hdrCell As Range, col As Long, startRow As Long, lastRow As Long
    Dim r As Long, txt As String
    Dim exportRowNo As Long
    Dim errCount As Long
    
    Set ws = ActiveSheet
    Set outWs = Worksheets("チェック結果")
    
    ' 出力開始行（E列の最後の下）
    exportRowNo = outWs.Cells(outWs.Rows.Count, "E").End(xlUp).Row + 1
    
    ' 「メール本文」列を探す
    Set hdrCell = ws.Rows(1).Find(What:=HEADER, LookAt:=xlWhole)
    If hdrCell Is Nothing Then
        MsgBox "列見出し「" & HEADER & "」が見つかりません。", vbExclamation
        Exit Sub
    End If
    col = hdrCell.Column
    
    ' データ範囲
    startRow = 3   ' 2行目はサンプルだからスキップ
    lastRow = ws.Cells(ws.Rows.Count, col).End(xlUp).Row
    
    ' 本文チェック
    For r = startRow To lastRow
        txt = CStr(ws.Cells(r, col).Value)
        
        ' Unicode→SJIS→Unicode でラウンドトリップ検証
        Dim b() As Byte, txt2 As String
        On Error Resume Next
        b = StrConv(txt, vbFromUnicode, 932)   ' SJIS化
        txt2 = StrConv(b, vbUnicode, 932)      ' 戻す
        On Error GoTo 0
        
        If txt <> txt2 Then
            outWs.Range("E" & exportRowNo).Value = txt
            outWs.Range("F" & exportRowNo).Value = _
                "表行:" & r & " にSJISで保存できない文字あり"
            exportRowNo = exportRowNo + 1
            errCount = errCount + 1
        End If
    Next r
    
    ' 結果メッセージ
    If errCount = 0 Then
        MsgBox "本文中にSJIS不可文字は見つかりませんでした。", vbInformation
    Else
        MsgBox "本文中にSJIS不可文字が " & errCount & " 件見つかりました。" & vbCrLf & _
               "詳細はチェック結果シートをご確認ください。", vbExclamation
    End If
End Sub
